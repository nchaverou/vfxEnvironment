/***************************************************************************
*                                                                          *
*  Copyright (C) Golaem S.A.                - All Rights Reserved.         *
*                                                                          *
***************************************************************************/

//**************************************************************************
/*! \file Maya Utils
	\brief Maya Related Functions
*/ //***********************************************************************

//************************************************************
/*! @name Node Utils
*/ //*********************************************************
//@{
//------------------------------------------------------------
//! reate Camera From Viewpoint
//------------------------------------------------------------
global proc ncCreateCameraFromViewpoint()
{
	string $camera;

	// First get the panel that currently has focus
	string $panel = `getPanel -wf`;

	// Compare the panel's type to confirm it is a "modelPanel"
	if ( "modelPanel" == `getPanel -to $panel` )
	{
		// Get the name of the camera using the ‘modelEditor’ command
		$camera = `modelEditor -q -camera $panel`;
	}

	// creates camera from the selected view i.e current focus
	duplicate -st $camera;

	$strAll=`ls`;
	$szLast=`size($strAll)`;
	string $strLast=$strAll[$szLast-2];
	pickWalk -d up;
	int $mods = `getModifiers`;
	$strAll=`ls`;
	$szLast=`size($strAll)`;
	string $strLast=$strAll[$szLast-2];
	select $strLast;
	ShowSelectedObjects;
	if ($mods == 8)
	{
		FrameSelected;
	}
}

//------------------------------------------------------------
//! Lock transform of selected objects
//------------------------------------------------------------
global proc ncLockSelectedTransform()
{
	string $selection[] = `ls -sl`;
	for ($item in $selection)
	{
		setAttr -l true ($item+".t");
		setAttr -l true ($item+".r");
		setAttr -l true ($item+".s");
	}
}

//------------------------------------------------------------
//! Freeze transform of selected objects
//------------------------------------------------------------
global proc ncFreezeSelectedTransform()
{
	makeIdentity -apply true -t 1 -r 1 -s 1 -n 0;
}

//------------------------------------------------------------
//! Hide/Show Normals
//------------------------------------------------------------
global proc ncHideShowNormals()
{
	int $hidden[] = `polyOptions -q -dn`;
	polyOptions -dn (!$hidden[0]);
}

//------------------------------------------------------------
//! Delete unknown nodes
//------------------------------------------------------------
global proc ncDeleteUnusedNodes()
{
	string $unknowNodes[] = `ls -type unknown -type unknownDag -type unknownTransform`;
	if (size($unknowNodes)) delete $unknowNodes;
	MLdeleteUnused; // delete unused nodes from Hypershade
}
//@}

//************************************************************
/*! @name UI Utils
*/ //*********************************************************
//@{
//------------------------------------------------------------
//! setShadedDisplay
//------------------------------------------------------------
global proc ncSetShadedDisplay()
{
	string $currentPanel = `getPanel -withFocus`;
    if ("" != $currentPanel)
    {
        string $panelType = `getPanel -typeOf $currentPanel`;
        if ($panelType ==  "modelPanel")
            modelEditor -edit -displayAppearance "smoothShaded" -displayTextures off -displayLights "default" $currentPanel;
    }
}

//------------------------------------------------------------
//! Set Maya Playback Range
//------------------------------------------------------------
global proc ncSetPlaybackRange(int $playbackMin, int $playbackMax)
{
	optionVar -fv "playbackMin" $playbackMin -fv "playbackMinDefault" $playbackMin -fv "playbackMinRange" $playbackMin -fv "playbackMinRangeDefault" $playbackMin;
	optionVar -fv "playbackMax" $playbackMax -fv "playbackMaxDefault" $playbackMax -fv "playbackMaxRange" $playbackMax -fv "playbackMaxRangeDefault" $playbackMax;
}

//------------------------------------------------------------
//! Disable Color Management
//------------------------------------------------------------
global proc ncDisableColorManagement()
{
	colorManagementPrefs -edit -cmEnabled 0;
}

//------------------------------------------------------------
//! Set Grid and Background prefs
//------------------------------------------------------------
global proc ncSetViewportAndGridDisplay(int $gradient, int $gridId, int $gridAxis, int $gridHighlight)
{
 	displayPref -displayGradient $gradient;
 	displayColor -dormant "grid" $gridId;
 	displayColor -dormant "gridAxis" $gridAxis;
	displayColor -dormant "gridHighlight" $gridHighlight;
}
//@}

//************************************************************
/*! @name Project Utils
*/ //*********************************************************
//@{
//------------------------------------------------------------
//! Set/Change Maya project
//------------------------------------------------------------
global proc ncSetMayaProject()
{
	string $result[] = `fileDialog2 -fileMode 3 -cap "Select Project Directory"`;
	if (size($result))
	{
		setProject $result[0];
	}
}

//------------------------------------------------------------
//! Freeze transform of selected objects
//------------------------------------------------------------
global proc ncOpenMayaRenderLog()
{
	string $mayaApp = `getenv "MAYA_APP_DIR"`;
	exec ("notepad "+$mayaApp+"/MayaRenderLog.txt");
}

//------------------------------------------------------------
//! Freeze transform of selected objects
//------------------------------------------------------------
global proc ncOpenUserSetup()
{
	string $maya_script_path = `getenv "MAYA_SCRIPT_PATH"`;
	string $maya_script_paths[];
	tokenize( $maya_script_path, ";", $maya_script_paths );

	for ($item in $maya_script_paths)
	{
		string $userScriptFilePath = ($item + "/userSetup.mel");
		if (`filetest -r $userScriptFilePath`)
		{
			exec ("notepad "+$userScriptFilePath);
			break;
		}
	}
}

//------------------------------------------------------------
//! Open the project directory
//------------------------------------------------------------
global proc ncOpenProjectDir()
{
	/*string $maya_project = `getenv "MAYA_PROJECT"`;*/
	string $maya_project = `workspace -q -rd`;
	$maya_project = substituteAllString($maya_project, "/", "\\");
	exec ("explorer.exe \""+$maya_project + "\"");
}

//------------------------------------------------------------
//! Open the scene directory
//------------------------------------------------------------
global proc ncOpenSceneDir()
{
	string $dirName = dirname(`file -q -sn -wcn`);
	$dirName = substituteAllString($dirName, "/", "\\");
	exec ("explorer.exe \""+$dirName + "\"");
}

//------------------------------------------------------------
//! Open the last opened file
//------------------------------------------------------------
global proc ncOpenLastOpenedFile()
{
	string $recentFiles[] = `optionVar -q RecentFilesList`;
	int $nbFiles = size($recentFiles)-1;
	file -f -options "v=0" -o $recentFiles[$nbFiles];
}
//@}

//************************************************************
/*! @name Tools Utils
*/ //*********************************************************
//@{
//------------------------------------------------------------
//! Plugin Loader
/*! \param
*/ //---------------------------------------------------------
global proc int ncLoadPlugin(string $pluginName)
{
	if (`pluginInfo -q -loaded $pluginName` == 0)
	{
		if (catch(`loadPlugin -qt $pluginName`) == 0)
		{
			print ($pluginName+" plugin correctly loaded\n");
			return 1;
		}

		warning ($pluginName+" not found");
		return 0;
	}
	else
	{
		warning ($pluginName+" is already loaded dumb-ass !!");
		return 0;
	}
}

//------------------------------------------------------------
//! openCommandPort
//------------------------------------------------------------
global proc ncOpenCommandPort(int $melPort, int $pythonPort)
{
	if (`about -batch` == 1) return;
	python("import maya.cmds as cmds");
	python("if not cmds.commandPort(\":"+$melPort+"\", q=True):\n\tcmds.commandPort(name=\":"+ $melPort +"\", sourceType=\"mel\")");
	python("if not cmds.commandPort(\":"+$pythonPort+"\", q=True):\n\tcmds.commandPort(name=\":"+ $pythonPort +"\", sourceType=\"python\")");
}
